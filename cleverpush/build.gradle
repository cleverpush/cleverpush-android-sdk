apply plugin: 'com.android.library'
apply plugin: 'com.vanniktech.maven.publish'
apply plugin: 'jacoco'


ext {
    buildVersions = [
            minSdkVersion: 19
    ]
}

android {
    namespace 'com.cleverpush'
    compileSdkVersion rootProject.buildVersions.compileSdkVersion

    defaultConfig {
        minSdkVersion buildVersions.minSdkVersion
        consumerProguardFiles 'consumer-proguard-rules.pro'
    }

    buildTypes {
        debug {
            minifyEnabled false
            testCoverageEnabled true
        }

        release {
            minifyEnabled false
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
        unitTests.all {
            useJUnitPlatform()
            // Disable unit tests but keep coverage collection  
            enabled = false
            // Configure Jacoco
            jacoco {
                includeNoLocationClasses = true
                excludes = ['jdk.internal.*']
            }
        }
    }

    sourceSets {
        main {
            res {
                srcDirs 'src\\main\\res', 'src\\test'
            }
            resources {
                srcDirs 'src\\main\\resources'
            }
            assets {
                srcDirs 'src\\main\\assets', 'src\\main\\assets\\2'
            }
        }
    }
}

dependencies {
    compileOnly fileTree(dir: 'libs', include: ['*.jar'])

    // Androidx
    api 'androidx.core:core:1.6.0'
    api 'androidx.annotation:annotation:1.3.0'
    api 'androidx.dynamicanimation:dynamicanimation:1.0.0'
    api 'androidx.constraintlayout:constraintlayout:2.1.4'
    api 'androidx.recyclerview:recyclerview:1.2.1'

    // Gson
    api 'com.google.code.gson:gson:2.9.1'

    // Google & Firebase
    implementation 'com.google.android.gms:play-services-base:[10.2.1, 18.0.99]'
    implementation 'com.google.android.gms:play-services-location:[16.0.0, 21.0.99]'
    api 'com.google.firebase:firebase-messaging:[10.2.1, 24.0.99]'

    // Huawei
    compileOnly "com.huawei.hms:push:$huaweiHmsPushVersion"

    // Android Material Components
    api 'com.google.android.material:material:1.4.0'

    // Glide
    api 'com.github.bumptech.glide:glide:4.15.1'

    // Circle Image View
    api 'de.hdodenhof:circleimageview:3.1.0'

    // Room Database
    implementation "androidx.room:room-runtime:2.5.2"
    annotationProcessor "androidx.room:room-compiler:2.5.2"
    testImplementation "androidx.room:room-testing:2.5.2"

    // Junit5
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.3.2'

    testImplementation 'com.google.truth:truth:1.1.3'
    testImplementation 'org.mockito:mockito-inline:3.4.0'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.9.1'
    testImplementation 'org.json:json:20180813'

    testImplementation 'org.awaitility:awaitility:4.1.0'
}

// Jacoco configuration
jacoco {
    toolVersion = rootProject.ext.jacocoVersion
}

// Add Jacoco test report task
tasks.register('jacocoTestReport', JacocoReport) {
    dependsOn 'testDebugUnitTest'
    
    reports {
        xml.required = true
        html.required = true
    }
    
    def fileFilter = [
        '**/R.class',
        '**/R$*.class',
        '**/BuildConfig.*',
        '**/Manifest*.*',
        '**/*Test*.*',
        'android/**/*.*'
    ]
    
    def debugTree = fileTree(dir: "${buildDir}/intermediates/javac/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"
    
    sourceDirectories.setFrom files([mainSrc])
    classDirectories.setFrom files([debugTree])
    executionData.setFrom fileTree(dir: buildDir, includes: [
        'outputs/unit_test_code_coverage/debugUnitTest/testDebugUnitTest.exec',
        'jacoco/testDebugUnitTest.exec'
    ])
}
